# Maintainer: Maxim Reznik <reznikmm@gmail.com>

_realname=aws
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgrel=1
pkgver=2016
pkgdesc="A complete embeddable Web application framework for Ada."
arch=('any')
license=('GPL3')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc-ada"
             "${MINGW_PACKAGE_PREFIX}-gprbuild"
            )
depends=("${MINGW_PACKAGE_PREFIX}-gcc-ada"
         )
url="http://libre.adacore.com/tools/aws"
source=(# aws-gpl-2016-src.tar.gz
        "http://mirrors.cdn.adacore.com/art/57399112c7a447658d00e1cd")
sha256sums=('310d7a79fba1cf622f6c8d53103226422c788126198911efef09e372d559db32')

fix_gnat_dll()
{
   # libgnat-6.dll.a and libgnarl-6.dll.a provided by mingw gcc don't work
   # remove them and copy dll-s in place

   libgcc=`gcc -print-libgcc-file-name`
   adalib=`dirname ${libgcc}`/adalib
   if [ -f ${adalib}/libgnat-6.dll.a ]; then
      gcc_bin=`which gcc`
      bin_dir=`dirname ${gcc_bin}`
      echo "Deleting *.dll.a:" rm -f ${adalib}/libgna{t,rl}-6.dll.a
      rm -f ${adalib}/libgna{t,rl}-6.dll.a
      cp ${bin_dir}/libgna{t,rl}-6.dll ${adalib}/
   fi
}

prepare()
{
  cd ${srcdir}/${_realname}-gpl-${pkgver}-src
  fix_gnat_dll
}

build() {
  cd ${srcdir}/${_realname}-gpl-${pkgver}-src
  make setup build
}

package() {
  cd ${srcdir}/${_realname}-gpl-${pkgver}-src
  make install prefix="${pkgdir}${MINGW_PREFIX}"

  # Copy License Files
  mkdir -p ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}
  cp -pf COPYING* ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/
}
